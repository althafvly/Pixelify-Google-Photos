plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 31

    def getVersionName = { ->
        def versionPropsFile = file('version.properties')
        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))

            def value = 0

            def runTasks = gradle.startParameter.taskNames
            if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
                value = 1;
            }

            def versionMajor = 1
            def versionMinor = 3
            def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            def versionNumber = versionProps['VERSION_NUMBER'].toInteger() + value

            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps['VERSION_NUMBER'] = versionNumber.toString()

            versionProps.store(versionPropsFile.newWriter(), null)

            return "${versionMajor}.${versionMinor}-${versionBuild}"
        } else {
            throw new GradleException("Could not read version.properties!")
        }
    }

    defaultConfig {
        applicationId "balti.xposed.pixelifyplus"
        minSdk 21
        targetSdk 31
        versionCode 5
        versionName getVersionName()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def formattedDate = new Date().format('yyyy-MM-dd')
            outputFileName = new File("Pixelify+_" + variant.versionName + "_" + formattedDate + ".apk");
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'

    // Xposed API
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
}